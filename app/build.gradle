apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.adarshr.test-logger'

android {
    compileSdkVersion 29

    // Get API keys from 'secret.properties' file to avoid exposing in VCS
    def secretProperties = new Properties()
    secretProperties.load(project.rootProject.file('secret.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.miniweather"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "API_KEY", secretProperties.getProperty("API_KEY"))
        buildConfigField "String", "API_BASE_URL", '"https://api.openweathermap.org/"'
        buildConfigField "String", "IMAGE_BASE_URL", '"https://openweathermap.org/img/wn/"'

        // https://developer.android.com/training/testing/junit-runner
        testBuildType "localTest"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
        }

        localTest {
            applicationIdSuffix ".localtest"
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug

            buildConfigField "String", "API_BASE_URL", '"http://localhost:9999/"'
            buildConfigField "String", "IMAGE_BASE_URL", '"http://invalidhost:9999/"'
            buildConfigField "String", "API_KEY", '"xxxxxxxxxxxxxxxxxxxxx"'
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Use view binding
    // https://developer.android.com/topic/libraries/view-binding
    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Compiler annotations for room
    // https://developer.android.com/jetpack/androidx/releases/room#compiler-options
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.incremental", "true")
            arg("room.expandProjection", "true")
        }
    }

    // Include shared folders in unit tests and android tests
    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
        unitTests {
            // For Robolectric
            // http://robolectric.org/getting-started/
            includeAndroidResources = true
        }
    }

    // Exclude unnecessary coroutines files in apk
    // https://github.com/Kotlin/kotlinx.coroutines/issues/2274
    packagingOptions {
        exclude "DebugProbesKt.bin"
    }

}

dependencies {
    // Kotlin
    def coroutines_version = "1.4.0"
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // AndroidX, UI
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.activity:activity-ktx:1.2.0-beta01'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-beta01'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.3'

    // Dependency injection
    // https://developer.android.com/training/dependency-injection/dagger-android
    def dagger_version = "2.29.1"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation 'com.google.android.material:material:1.2.1'

    // Location services
    // https://developer.android.com/training/location/retrieve-current#setup
    implementation 'com.google.android.gms:play-services-location:17.1.0'

    // Http requests
    // https://github.com/square/retrofit
    def okhttp_version = "4.9.0"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    // Image loading
    // https://github.com/bumptech/glide
    def glide_version = "4.9.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // Room db
    // https://developer.android.com/training/data-storage/room
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Annotation processors
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    // Testing
    // https://developer.android.com/training/testing
    def androidx_test_version = "1.3.0"
    def androidx_junit_version = "1.1.2"
    def espresso_version = "3.3.0"
    testImplementation 'junit:junit:4.13'
    testImplementation "androidx.test:core:$androidx_test_version"
    testImplementation "androidx.test:rules:$androidx_test_version"
    testImplementation "androidx.test.ext:junit:$androidx_junit_version"
    testImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    testImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.mockito:mockito-inline:3.5.11'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    androidTestImplementation "androidx.test:core:$androidx_test_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    androidTestUtil 'androidx.test:orchestrator:1.3.0'
}
