apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.adarshr.test-logger'

android {
    compileSdkVersion 30

    // Get API keys from 'secret.properties' file to avoid exposing in VCS
    def secretProperties = new Properties()
    secretProperties.load(project.rootProject.file('secret.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.miniweather"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "WEATHER_API_BASE_URL", '"https://api.openweathermap.org/"'
        buildConfigField "String", "WEATHER_IMAGE_BASE_URL", '"https://openweathermap.org/img/wn/"'
        buildConfigField("String", "WEATHER_API_KEY", secretProperties.getProperty("WEATHER_API_KEY"))

        // https://developer.android.com/training/testing/junit-runner
        // https://developer.android.com/training/dependency-injection/dagger-android#dagger-end-to-end-tests
        testInstrumentationRunner "com.miniweather.testutil.IntegrationTestRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Use view binding
    // https://developer.android.com/topic/libraries/view-binding
    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Compiler annotations for room
    // https://developer.android.com/jetpack/androidx/releases/room#compiler-options
    kapt {
        correctErrorTypes true
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.incremental", "true")
            arg("room.expandProjection", "true")
        }
    }

    // Include shared folders in unit tests and android tests
    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
            assets.srcDirs += "src/sharedTest/assets"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
            resources.srcDirs += "src/sharedTest/assets"
        }
        debug {
            assets.srcDirs += "src/androidTest/assets"
        }
    }

    testBuildType "debug"
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
        unitTests {
            // For Robolectric
            // http://robolectric.org/getting-started/
            includeAndroidResources = true
        }
    }

    // Exclude unnecessary coroutines files in apk
    // https://github.com/Kotlin/kotlinx.coroutines/issues/2274
    packagingOptions {
        exclude "DebugProbesKt.bin"
    }

}

dependencies {
    // Kotlin
    def coroutines_version = "1.5.0"
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version"

    // AndroidX, UI
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.activity:activity-ktx:1.3.0-beta01'
    implementation 'androidx.fragment:fragment-ktx:1.4.0-alpha02'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // Material Design
    implementation 'com.google.android.material:material:1.3.0'

    // JetPack Navigation
    def navigation_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Dependency injection
    // https://developer.android.com/training/dependency-injection/dagger-android
    def dagger_version = "2.29.1"
    implementation "com.google.dagger:dagger:$dagger_version"

    // Location services
    // https://developer.android.com/training/location/retrieve-current#setup
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    // Http requests
    // https://github.com/square/retrofit
    def okhttp_version = "4.9.0"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    // Image loading
    // https://github.com/bumptech/glide
    def glide_version = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // Room db
    // https://developer.android.com/training/data-storage/room
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Annotation processors
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

    // Testing
    // https://developer.android.com/training/testing
    def androidx_test_version = "1.3.0"
    def androidx_junit_version = "1.1.2"
    def espresso_version = "3.3.0"
    def mockk_version = "1.11.0"
    def truth_version = "1.1.3"

    // JUnit
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.test.ext:junit:$androidx_junit_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_version"

    // AndroidX libraries
    testImplementation "androidx.test:core:$androidx_test_version"
    testImplementation "androidx.test:rules:$androidx_test_version"
    androidTestImplementation "androidx.test:core:$androidx_test_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_version"
    androidTestUtil "androidx.test:orchestrator:$androidx_test_version"
    debugImplementation "androidx.fragment:fragment-testing:1.3.4"

    // Espresso UI assertions
    testImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    testImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"

    // Room testing components
    testImplementation "androidx.room:room-testing:$room_version"

    // Robolectric tests on JVM
    testImplementation 'org.robolectric:robolectric:4.5.1'

    // Coroutines testing
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Assertions
    testImplementation "com.google.truth:truth:$truth_version"

    // Kotlin mocking
    testImplementation "io.mockk:mockk:$mockk_version"
    // Needed until inline classes are supported in MockK
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // Causes random integration test failures on API 28 if the test takes too long?
    // May be fixed by x64 emulators or higher APIs
    // https://github.com/mockk/mockk/issues/466
    // https://github.com/mockk/mockk/blob/master/ANDROID.md
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"

    // Mock web server
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

}

apply from: 'testScreenshots.gradle'
