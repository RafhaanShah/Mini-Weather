apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    // Get API keys from 'secret.properties' file to avoid exposing in VCS
    def secretProperties = new Properties()
    secretProperties.load(project.rootProject.file('secret.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.miniweather"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "API_KEY", secretProperties.getProperty("API_KEY"))
        buildConfigField "String", "API_BASE_URL", '"https://api.openweathermap.org/"'
        buildConfigField "String", "IMAGE_BASE_URL", '"https://openweathermap.org/img/wn/"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Core AndroidX libraries and UI
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'

    // Dependency injection and location services
    implementation 'com.google.dagger:dagger:2.29.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.android.gms:play-services-location:17.1.0'

    // Http requests and image loading
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // Annotation processors
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    kapt 'com.google.dagger:dagger-compiler:2.29.1'
    kaptTest 'com.google.dagger:dagger-compiler:2.29.1'

    // Test running, mocking, and enabling Android tests on JVM without emulators
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'org.mockito:mockito-inline:3.5.11'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "org.robolectric:robolectric:4.4"

}
