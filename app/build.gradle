apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.adarshr.test-logger'

android {
    compileSdkVersion 29

    // Get API keys from 'secret.properties' file to avoid exposing in VCS
    def secretProperties = new Properties()
    secretProperties.load(project.rootProject.file('secret.properties').newDataInputStream())

    defaultConfig {
        applicationId "com.miniweather"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "API_KEY", secretProperties.getProperty("API_KEY"))
        buildConfigField "String", "API_BASE_URL", '"https://api.openweathermap.org/"'
        buildConfigField "String", "IMAGE_BASE_URL", '"https://openweathermap.org/img/wn/"'
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.incremental", "true")
            arg("room.expandProjection", "true")
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        exclude "DebugProbesKt.bin"
    }

}

dependencies {
    def coroutines_version = "1.4.0-M1"
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // AndroidX, UI
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'

    // Dependency injection
    def dagger_version = "2.29.1"
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation 'com.google.android.material:material:1.2.1'

    // Location services
    implementation 'com.google.android.gms:play-services-location:17.1.0'

    // Http requests, image loading
    def glide_version = "4.9.0"
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // Room db
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Annotation processors
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    // Testing libraries
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.mockito:mockito-inline:3.5.11'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

}
